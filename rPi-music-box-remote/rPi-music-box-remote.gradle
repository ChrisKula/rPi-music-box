apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0

def gitRevisionShortSha = getGitRevisionShortSha()

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.christiankula.rpimusicbox.remote"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode computeVersionCode(versionMajor, versionMinor, versionPatch)
        versionName computeVersionName(versionMajor, versionMinor, versionPatch)

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            def debugSuffix = ".debug"

            versionNameSuffix debugSuffix
            applicationIdSuffix debugSuffix

            resValue "string", "app_version", "${defaultConfig.versionName}${debugSuffix}"
            resValue "string", "git_revision_short_sha", "${gitRevisionShortSha}"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue "string", "app_version", "${defaultConfig.versionName}"
            resValue "string", "git_revision_short_sha", "${gitRevisionShortSha}"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(":androidcommons")
    implementation project(':rxnearby')

    implementation libraries.androidArchLifecycle

    implementation libraries.appCompat

    implementation libraries.constraintLayout

    implementation libraries.dagger
    kapt libraries.daggerCompiler

    implementation libraries.daggerAndroidSupport
    kapt libraries.daggerAndroidProcessor

    implementation libraries.kotlinStdLib

    implementation libraries.rxJava2
    implementation libraries.rxJava2Android
    implementation libraries.rxJava2Kotlin

    implementation libraries.timber

    // Test dependencies
    testImplementation testLibraries.junit
    androidTestImplementation testLibraries.androidTestRunner
    androidTestImplementation testLibraries.espresso
}
